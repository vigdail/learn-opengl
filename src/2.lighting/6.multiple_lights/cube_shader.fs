#version 330 core

in vec3 Normal;
in vec3 fragPos;
in vec2 uv;

out vec4 fragColor;

#define POINT_LIGHTS_COUNT 4

struct Material {
   sampler2D diffuse;
   sampler2D specular;
   float shininess;
};

struct DirLight {
   vec3 direction;
   
   vec3 ambient;
   vec3 diffuse;
   vec3 specular;
};

struct PointLight {
   vec3 position;

   vec3 ambient;
   vec3 diffuse;
   vec3 specular;

   float constant;
   float linear;
   float quadratic;
};

struct SpotLight {
   vec3 position;
   vec3 direction;
   float cutOff;
   float outerCutOff;
   vec3 ambient;
   vec3 diffuse;
   vec3 specular;

   float constant;
   float linear;
   float quadratic;
};

uniform vec3 cameraPosition;
uniform Material material;

uniform DirLight dirLight;
uniform PointLight pointLights[POINT_LIGHTS_COUNT];
uniform SpotLight spotLight;

vec3 calcDirLight(DirLight light, vec3 normal, vec3 viewDir) {
   vec3 lightDir = normalize(-light.direction);

   float diff = max(dot(normal, lightDir), 0.0);
   vec3 reflectDir = reflect(-lightDir, normal);
   float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);

   vec3 ambient = light.ambient * vec3(texture(material.diffuse, uv));
   vec3 diffuse = light.diffuse * diff * vec3(texture(material.diffuse, uv));
   vec3 specular = light.specular * spec * vec3(texture(material.specular, uv));

   return ambient + diffuse + specular;
}

vec3 calcPointLight(PointLight light, vec3 normal, vec3 fragPos, vec3 viewDir) {
   vec3 lightDir = normalize(light.position - fragPos);
   
   float diff = max(dot(normal, lightDir), 0.0);
   vec3 reflectDir = reflect(-lightDir, normal);
   float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);

   float distance = length(light.position - fragPos);
   float attenuation = 1.0 / (light.constant + distance * light.linear + distance * distance * light.quadratic);

   vec3 ambient = light.ambient * vec3(texture(material.diffuse, uv));
   vec3 diffuse = light.diffuse * diff * vec3(texture(material.diffuse, uv));
   vec3 specular = light.specular * spec * vec3(texture(material.specular, uv));

   ambient *= attenuation;
   diffuse *= attenuation;
   specular *= attenuation;

   return ambient + diffuse + specular;
}

vec3 calcSpotLight(SpotLight light, vec3 normal, vec3 fragPos, vec3 viewDir) {
   vec3 lightDir = normalize(light.position - fragPos);

   float diff = max(dot(normal, lightDir), 0.0);
   vec3 reflectDir = reflect(-lightDir, normal);
   float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);

   float distance = length(light.position-fragPos);
   float attenuation = 1.0 / (light.constant + distance * light.linear + distance * distance * light.quadratic);

   float theta = dot(lightDir, normalize(-light.direction)); 
   float epsilon = light.cutOff - light.outerCutOff;
   float intensity = clamp((theta - light.outerCutOff) / epsilon, 0.0, 1.0);

   vec3 ambient = light.ambient * vec3(texture(material.diffuse, uv)).rgb;
   vec3 diffuse = light.diffuse * diff * vec3(texture(material.diffuse, uv)).rgb;
   vec3 specular = light.specular * spec * vec3(texture(material.specular, uv)).rgb;

   return (ambient + diffuse + specular) * intensity * attenuation;
}

void main() {

   vec3 norm = normalize(Normal);
   vec3 viewDir = normalize(cameraPosition - fragPos);
   vec3 result = vec3(0.0);

   result += calcDirLight(dirLight, norm, viewDir);

   for (int i = 0; i < POINT_LIGHTS_COUNT; i++) {
      result += calcPointLight(pointLights[i], norm, fragPos, viewDir);
   }

   result += calcSpotLight(spotLight, norm, fragPos, viewDir);

   fragColor = vec4(result, 1.0);
}